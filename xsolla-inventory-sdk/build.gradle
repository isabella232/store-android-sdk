apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion sdk_compile

    resourcePrefix 'xsolla_inventory_'

    defaultConfig {
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        versionCode inventory_sdk_version_code
        versionName inventory_sdk_version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        quiet true
        abortOnError true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.4'
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            moduleName.set("Xsolla Inventory SDK for Android")
            includes.from("Module.md")
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.xsolla.android'
    PUBLISH_ARTIFACT_ID = 'inventory'
    PUBLISH_VERSION = inventory_sdk_version_name
    PUBLISH_NAME = 'Xsolla Inventory SDK for Android'
    PUBLISH_DESCRIPTION = 'Xsolla Inventory SDK for Android is used to integrate applications based on Android with Player Inventory for managing: user inventory, virtual currency balance, cross-platform inventory'
}

apply from: "${rootDir}/publishMavenCentral.gradle"

apply from: 'tests.gradle'