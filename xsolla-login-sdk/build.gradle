apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion sdk_compile

    resourcePrefix 'xsolla_login_'

    defaultConfig {
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        versionCode login_sdk_version_code
        versionName login_sdk_version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        quiet true
        abortOnError true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.4'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.browser:browser:1.3.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'com.facebook.android:facebook-login:8.1.0'
    testImplementation 'com.google.android.gms:play-services-auth:18.0.0'
    testImplementation 'com.auth0.android:jwtdecode:2.0.0'
    testImplementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.6.19'
    testImplementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly 'com.facebook.android:facebook-login:8.1.0'
    compileOnly 'com.google.android.gms:play-services-auth:18.0.0'
    compileOnly 'com.auth0.android:jwtdecode:2.0.0'
    compileOnly 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.6.19'
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            moduleName.set("Xsolla Login SDK for Android")
            includes.from("Module.md")
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.xsolla.android'
    PUBLISH_ARTIFACT_ID = 'login'
    PUBLISH_VERSION = login_sdk_version_name
    PUBLISH_NAME = 'Xsolla Login SDK for Android'
    PUBLISH_DESCRIPTION = 'Xsolla Login SDK for Android is used to integrate Xsolla Login, a single sign-on tool that uses API methods to authenticate and secure user passwords. This creates a seamless one-click registration experience players can use for fast and safe transactions across all of your games.'
}

apply from: "${rootDir}/publishMavenCentral.gradle"