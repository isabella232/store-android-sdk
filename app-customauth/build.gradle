apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion sdk_compile

    signingConfigs {
        release {
            file("../signing.properties").with { propFile ->
                if (propFile.canRead()) {
                    def properties = new Properties()
                    properties.load(new FileInputStream(propFile))

                    storeFile file(properties['keystorePath'])
                    storePassword properties['keystorePassword']
                    keyAlias properties['keyAlias']
                    keyPassword properties['keyPassword']
                } else {
                    println 'Unable to read signing.properties'
                }
            }
        }
    }

    defaultConfig {
        applicationId "com.xsolla.android.customauth"
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        versionCode sample_app_version_code
        versionName sample_app_version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'int', 'PROJECT_ID', '77640'
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField 'boolean', 'IS_SANDBOX', 'true'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField 'boolean', 'IS_SANDBOX', 'true'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding = true
    }
}

androidExtensions {
    features = ["parcelize"]
}

dependencies {
    // Core
    implementation project(":app-core")
    implementation project(":xsolla-inventory-sdk")
    implementation project(":xsolla-store-sdk")
    implementation project(":xsolla-payments-sdk")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // ViewBinding
    implementation 'com.kirich1409.viewbindingpropertydelegate:viewbindingpropertydelegate:1.3.1'

    // Network
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.squareup.retrofit2:converter-gson:2.6.4'
    implementation "com.squareup.okhttp3:okhttp:4.8.1"

}